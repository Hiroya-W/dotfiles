function fish_greeting
    echo ""
end

# cd + ls にする
function cd
    if test (count $argv) -eq 0
        builtin cd
    else if test (count $argv) -gt 1
        printf "%s\n" (_ "Too many args for cd command")
        return 1
    end
    # Avoid set completions.
    set -l previous $PWD

    if test "$argv" = "-"
        if test "$__fish_cd_direction" = "next"
            nextd
        else
            prevd
        end
        return $status
    end
    builtin cd $argv
    set -l cd_status $status
    # Log history
    if test $cd_status -eq 0 -a "$PWD" != "$previous"
        set -q dirprev[$MAX_DIR_HIST]
        and set -e dirprev[1]
        set -g dirprev $dirprev $previous
        set -e dirnext
        set -g __fish_cd_direction prev
    end

    if test $cd_status -ne 0
        return 1
    end
    ls
    return $status
end

# peco + ghq
function peco_ghq
    set -l query (commandline)

    if test -n $query
        set peco_flags --query "$query"
    end

    ghq list --full-path | peco $peco_flags | read recent
    if [ $recent ]
        cd $recent
        commandline -r ''
        commandline -f repaint
    end
end

function fish_user_key_bindings
    bind \c] peco_ghq
end

#set -g fish_key_bindings fish_vi_key_bindings
#bind -M insert \cc kill-whole-line repaint

alias exa "exa --icons"
alias ls "exa"

fish_add_path $HOME/go/bin

# source asdf
if test (uname -s) = "Darwin"
    # for macOS
    source /usr/local/opt/asdf/libexec/asdf.fish
else
    # for Linux
    # fish & pacman
    source /opt/asdf-vm/asdf.fish
end

eval (starship init fish)
